#include 'TOTVS.CH'
#include 'protheus.ch'
#include 'topconn.ch'

User Function Projeto02()

  Private oDlg1
  Private cTituloJanela := "Projeto01 - Primeira tela ADVPL"

  oDlg1 := TDialog():New(0,0,720,1280,cTituloJanela,,,,,CLR_BLACK,CLR_WHITE,,,.T.)

  // Folder
  Private oFolder
  Private aFolder

  aFolder := {'Dados Pessoais', 'Endereço', 'Outros'}
  oFolder := TFolder():New(0,0,aFolder,aFolder,oDlg1,,,,.T.,,600,200)

  // Variaveis Folder Dados Pessoais
  Private oSayCodigo
  Private oGetCodigo
  Private cGetCodigo
  
  Private oSayNome
  Private oGetNome
  Private cGetNome

  Private oSayNomeReduz
  Private oGetNomeReduz
  Private cGetNomeReduz

  Private oSayDatNasc
  Private oGetDatNasc
  Private dGetDatNasc

  Private oSayDDDCel
  Private oGetDDDCel
  Private cGetDDDCel

  Private oSayCEL
  Private oGetCEL
  Private cGetCEL

  Private oGroupSexo

  Private aSexo := {'Masculino', 'Feminino'}
  Private nSexo := 1
  Private oRadioSexo

  Private oSayCPF
  Private oGetCPF
  Private cGetCPF

  // Variaveis folder endereço

  Private oSayEndereco
  Private oGetEndereco
  Private cGetEndereco

  Private oSayBairro
  Private oGetBairro
  Private cGetBairro

  Private oSayImovel
  Private aImoveis       := {"Casa","Apartamento","Outro"}
  Private cImovel        := aImoveis[2]
  Private oComboImovel

  Private oSayCEP
  Private oGETCEP
  Private cGetCEP

  Private oSayEstado
  Private aEstados      := {"AC", "AL", "AM", "AP", "BA", "CE", "DF", "ES", "GO", "MA", "MT", "MS", "MG", "PA", "PB", "PR", "PE", "PI", "RJ", "RN", "RO", "RS", "RR", "SC", "SE", "SP", "TO"}
  Private cEstados      := aEstados[5]
  Private oComboEstado

  Private oSayCidade
  Private oGetCidade
  Private cGetCidade

  // Variaveis Folder Outros

  Private oSayEstCivil
  Private aEstCivil := {"Solteiro","Namorando","Casado","Protheuzando","Codificando","Vagando na net","Só no Insta","O pai tá on","Casado com Protheus","Namorando ADVPL"}
  Private cEstCivil  := aEstCivil[4]
  Private oComboEstCivil

  Private oGroupPet
  Private aPet := {"Sim","Não"}
  Private nPet  := 2
  Private oRadioPet

  Private oGroupLinguagens

  Private oCheckAdvpl
  Private oCheckSQL
  Private oCheckCSHARP
  Private oCheckPython

  Private lCheckAdvpl   := .F.
  Private lCheckSQL     := .F.    
  Private lCheckCSHARP  := .F.    
  Private lCheckPython  := .F.  

  Private oButtonMarkAll
  Private oButtonUnmarkAll

  Private oSayObservacao
  Private cObservacao := "Digite aqui as características de Protheuzeiro que te definem"
  Private oTMultigetObservacao
  
  // Variaveis Fora do folder 

  Private oButtonIncluir
  Private oButtonAlterar
  Private oButtonConsultar
  Private oButtonExcluir
  Private oButtonSair
  Private oButtonLimparTela

  Private oBmpIncluir
  Private oBmpAlterar
  Private oBmpConsultar
  Private oBmpExcluir
  Private oBmpSair
  Private oBmpLimparTela

  // Folder Dados Pessoais
  oSayCodigo := TSay():New(01,05,{||'Código'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
  cGetCodigo := Space(6)
  oGetCodigo := TGet():New(10,05,{|u| IF(Pcount()>0,cGetCodigo:=u,cGetCodigo)},oFolder:aDialogs[1],40,009,"@ 999999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCodigo,,,,)
  oGetCodigo:cF3 := 'SZ1'

  oSayNome := TSay():New(25,05,{||'Nome'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
  cGetNome := Space(30)
  oGetNome := TGet():New(35,05,{|u| IF(Pcount()>0,cGetNome:=u,cGetNome)},oFolder:aDialogs[1],096,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNome,,,,)

  oSayNomeReduz := TSay():New(25,110,{||'Nome Reduzido'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
  cGetNomeReduz := Space(30)
  oGetNomeReduz := TGet():New(35,110,{|u| IF(Pcount()>0,cGetNomeReduz:=u,cGetNomeReduz)},oFolder:aDialogs[1],60,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetNomeReduz,,,,)

  oSayDatNasc := TSay():New(25,180,{||'Data de nascimento'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
  dGetDatNasc := Date()
  oGetDatNasc := TGet():New(35,180,{|u| IF(Pcount()>0,dGetDatNasc:=u,dGetDatNasc)},oFolder:aDialogs[1],60,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,/**/,,,,.T.)

  oSayDDDCel := TSay():New(25,250,{||'DDD'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
  cGetDDDCel := Space(2)
  oGetDDDCel := TGet():New(35,250,{|u| IF(Pcount()>0,cGetDDDCel:=u,cGetDDDCel)},oFolder:aDialogs[1],20,009,"@E (99)",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetDDDCel,,,,.T.)

  oSayCEL := TSay():New(25,280,{||'Celular'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
  cGetCEL := Space(11)
  oGetCEL := TGet():New(35,280,{|u| IF(Pcount()>0,cGetCEL:=u,cGetCEL)},oFolder:aDialogs[1],40,009,"@E 9-9999-9999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCEL,,,,.T.)

  oGroupSexo := TGroup():New(25,330,60,430,'Sexo',oFolder:aDialogs[1],,,.T.)
  oRadioSexo := TRadMenu():New(30,340,aSexo,{|u| IF(Pcount()>0,nSexo:=u,nSexo)},oFolder:aDialogs[1],,,,,,,,60,20,,,,.T.)

  oSayCPF := TSay():New(50,05,{||'CPF'},oFolder:aDialogs[1],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
  cGetCPF := Space(14)
  oGetCPF := TGet():New(60,05,{|u| IF(Pcount()>0,cGetCPF:=u,cGetCPF)},oFolder:aDialogs[1],40,009,"@E 999.999.999-99",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCPF,,,,.T.)

  // Folder Endereço
  oSayEndereco     := TSay():New(01,05,{||'Endereço'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
  cGetEndereco     := Space(30)
  oGetEndereco     := TGet():New( 10,05,{|u| IIF(Pcount()>0,cGetEndereco:=u,cGetEndereco)},oFolder:aDialogs[2],096,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetEndereco,,,, )

  oSayBairro       := TSay():New(01,110,{||'Bairro'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
  cGetBairro       := Space(20)
  oGetBairro       := TGet():New( 10,110,{|u| IIF(Pcount()>0,cGetBairro:=u,cGetBairro)},oFolder:aDialogs[2],60,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetBairro,,,, )

  oSayImovel       := TSay():New(01,180,{||'Tipo de Imóvel'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
  oComboImovel     := tComboBox():New(010,180,{|u|if(PCount()>0,cImovel:=u,cImovel)},aImoveis,60,20,oFolder:aDialogs[2],,,,,,.T.,,,,,,,,,'cImovel')

  oSayCEP       := TSay():New(01,250,{||'CEP'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
  cGetCEP       := Space(9)
  oGETCEP       := TGet():New( 10,250,{|u| IIF(Pcount()>0,cGetCEP:=u,cGetCEP)},oFolder:aDialogs[2],30,009,"@E 99999-999",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCEP,,,, )

  oSayEstado    := TSay():New(25,05,{||'Estado'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
  oComboEstado  := tComboBox():New(35,05,{|u|if(PCount()>0,cEstados:=u,cEstados)},aEstados,40,20,oFolder:aDialogs[2],,,,,,.T.,,,,,,,,,'cEstado')

  oSayCidade     := TSay():New(25,50,{||'Cidade'},oFolder:aDialogs[2],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
  cGetCidade     := Space(30)
  oGetCidade     := TGet():New( 35,50,{|u| IIF(Pcount()>0,cGetCidade:=u,cGetCidade)},oFolder:aDialogs[2],096,009,"",,0,,,.F.,,.T.,,.F.,,.F.,.F.,,.F.,.F.,,cGetCidade,,,, )

  // Folder Outros

  oSayEstCivil       := TSay():New(01,05,{||'Estado Civil'},oFolder:aDialogs[3],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
  oComboEstCivil     := tComboBox():New(10,05,{|u|if(PCount()>0,cEstCivil:=u,cEstCivil)},aEstCivil,65,20,oFolder:aDialogs[3],,/**/,,,,.T.,,,,,,,,,'cCombo')

  oGroupPet          := tGroup():New(01,80,40,140,"Possui Pet?",oFolder:aDialogs[3],,,.T.)
  oRadioPet          := tRadMenu():New(10,85,aPet,{|u| iif(PCount()>0,nPet :=u,nPet )},oFolder:aDialogs[3],,,,,,,,30,20,,,,.T.)

  oGroupLinguagens   := tGroup():New(01,150,55,250,"Linguagens que domina:",oFolder:aDialogs[3],,,.T.)

  oCheckADVPL         := TCheckBox():New(10,160,'ADVPL',{|| lCheckADVPL},oFolder:aDialogs[3],100,210,,{|| lCheckADVPL := !lCheckADVPL},,,,,,.T.,,,)

  oCheckSQL           := TCheckBox():New(20,160,'SQL',,oFolder:aDialogs[3],100,210,,,,,,,,.T.,,,)
  oCheckSQL:bSetGet   := {|| lCheckSQL}
  oCheckSQL:bLClicked := {|| lCheckSQL := !lCheckSQL}

  oCheckCSHARP            := TCheckBox():New(30,160,'CSharp',,oFolder:aDialogs[3],100,210,,,,,,,,.T.,,,)
  oCheckCSHARP:bSetGet    := {|| lCheckCSHARP}
  oCheckCSHARP:bLClicked  := {|| lCheckCSHARP := !lCheckCSHARP}

  oCheckPython            := TCheckBox():New(40,160,'Python',,oFolder:aDialogs[3],100,210,,,,,,,,.T.,,,)
  oCheckPython:bSetGet    := {|| lCheckPython}
  oCheckPython:bLClicked  := {|| lCheckPython := !lCheckPython}

  oButtonMarkAll := TButton():New(15,200,"Todas",oFolder:aDialogs[3],;
  {||lCheckADVPL := .T. ,lCheckSQL := .T., lCheckCSHARP := .T., lCheckPython := .T.}, 40,10,,,.F.,.T.,.F.,,.F.,,,.F. )

  oButtonUnmarkAll := TButton():New(30,200,"Nenhuma",oFolder:aDialogs[3],;
  {||lCheckADVPL := .F. ,lCheckSQL := .F., lCheckCSHARP := .F., lCheckPython := .F.}, 40,10,,,.F.,.T.,.F.,,.F.,,,.F. )

  oSayObervacao   := TSay():New(01,05,{||'Estado Civil'},oFolder:aDialogs[3],,,,,,.T.,CLR_BLUE,CLR_WHITE,300,20)
  oTMultiget1 := tMultiget():new(10,270,{|u|if(pCount()>0,cObservacao:=u,cObservacao)},oFolder:aDialogs[3],200,100,,,,,,.T.)

  // Fora do Folder

  oButtonIncluir      := TButton():New(250, 20, "Incluir",    oDlg1,{|| IncluirSZ1()}, 40,15,,,.F.,.T.,.F.,,.F.,,,.F. ) 
  oButtonAlterar      := TButton():New(250, 80, "Alterar",    oDlg1,{|| AlterarSZ1()}, 40,15,,,.F.,.T.,.F.,,.F.,,,.F. ) 
  oButtonConsultar    := TButton():New(250, 140, "Consultar", oDlg1,{|| BuscarSZ1()}, 40,15,,,.F.,.T.,.F.,,.F.,,,.F. ) 
  oButtonExcluir      := TButton():New(250, 200, "Excluir",   oDlg1,{|| ExcluirSZ1()}, 40,15,,,.F.,.T.,.F.,,.F.,,,.F. ) 

  oBmpIncluir := TBitmap():New(220,30,142,142,,"C:\TOTVS12133\Protheus\Protheus\my projects\ADVPLII\BMPS DE BOTÓES\incluirprotheuzeiro.bmp",.T.,oDlg1,{|| IncluirSZ1()},,.F.,.F.,,,.F.,,.T.,,.F.)
  oBmpIncluir:lAutoSize := .T.

  oBmpAlterar := TBitmap():New(220,90,142,142,,"C:\TOTVS12133\Protheus\Protheus\my projects\ADVPLII\BMPS DE BOTÓES\alterarprotheuzeiro.bmp",.T.,oDlg1,{|| AlterarSZ1()},,.F.,.F.,,,.F.,,.T.,,.F.)
  oBmpAlterar:lAutoSize := .T.

  oBmpConsultar := TBitmap():New(220,150,142,142,,"C:\TOTVS12133\Protheus\Protheus\my projects\ADVPLII\BMPS DE BOTÓES\consultarprotheuzeiro.bmp",.T.,oDlg1,{|| BuscarSZ1()},,.F.,.F.,,,.F.,,.T.,,.F.)
  oBmpConsultar:lAutoSize := .T.

  oBmpExcluir := TBitmap():New(220,210,142,142,,"C:\TOTVS12133\Protheus\Protheus\my projects\ADVPLII\BMPS DE BOTÓES\excluirprotheuzeiro.bmp",.T.,oDlg1,{|| ExcluirSZ1()},,.F.,.F.,,,.F.,,.T.,,.F.)
  oBmpExcluir:lAutoSize := .T.

  // Dialog Final
  oDlg1:Activate(,,,.T.)

return

Static Function IncluirSZ1()

DbSelectArea('SZ1')

DbSetOrder(1)

if DbSeek(XFILIAL('SZ1')+cGetCodigo)
  Alert('Registro já existe!')
else
  RecLock('SZ1', .T.)

    SZ1 -> Z1_FILIAL := XFILIAL('SZ1')
    SZ1 -> Z1_COD := cGetCodigo
    SZ1 -> Z1_NOME := cGetNome
    SZ1 -> Z1_NOMERED := cGetNomeReduz
    SZ1 -> Z1_DATNASC := dGetDatNasc
    SZ1 -> Z1_DDDCEL :=  STRTRAN(STRTRAN(cGetDDDCel,'(',''),')','')
    SZ1 -> Z1_CELULAR := STRTRAN(cGetCEL,'-','')
    SZ1 -> Z1_SEXO := CValToChar(nSexo)  // 1 = Masculino / 2 = Feminino
    SZ1 -> Z1_CPF := STRTRAN(STRTRAN(cGetCPF,'.',''),'-','')
    SZ1 -> Z1_ENDEREC := cGetEndereco
    SZ1 -> Z1_BAIRRO := cGetBairro
    SZ1 -> Z1_TIPOIMO := cImovel
    SZ1 -> Z1_CEP := STRTRAN(cGetCEP,'-','')
    SZ1 -> Z1_ESTADO := cEstados
    SZ1 -> Z1_CIDADE := cGetCidade
    SZ1 -> Z1_ESTADOC := cEstCivil
    SZ1 -> Z1_TEMPET := CValToChar(nPet)
    SZ1 -> Z1_LADVPL := IIF(lCheckADVPL = .T.,"1","2") // 1 = sim, 2 = não
    SZ1 -> Z1_LSQL := IIF(lCheckSQL,"1","2")
    SZ1 -> Z1_LCSHARP := IIF(lCheckCSHARP,"1","2")
    SZ1 -> Z1_PYTHON := IIF(lCheckPython,"1","2")
    SZ1 -> Z1_OBS := cObservacao

  MsUnlock()

  MsgInfo('Cliente incluido com sucesso', 'Sucesso')
  Limpartela()
endif

DbCloseArea()
return

Static Function BuscarSZ1()


    DbSelectArea('SZ1')
    DbSetOrder(1)
    
    if DbSeek(XFILIAL('SZ1')+cGetCodigo)
      cGetCodigo := SZ1 -> Z1_COD
      cGetNome := SZ1 -> Z1_NOME
      cGetNomeReduz := SZ1 -> Z1_NOMERED
      dGetDatNasc := SZ1 -> Z1_DATNASC
      cGetDDDCel := '('+SZ1 -> Z1_DDDCEL+')'
      cGetCEL := SubStr(SZ1 -> Z1_CELULAR,1,1)+'-'+SubStr(SZ1 -> Z1_CELULAR,2,4)+'-'+SubStr(SZ1 -> Z1_CELULAR,6,4)
      nSexo := Val(SZ1 -> Z1_SEXO)
      cGetCPF := SubStr(SZ1 -> Z1_CPF,1,3)+'.'+SubStr(SZ1 -> Z1_CPF,4,3)+'.'+SubStr(SZ1 -> Z1_CPF,7,3)+'-'+SubStr(SZ1 -> Z1_CPF,10,2)
      cGetEndereco := SZ1 -> Z1_ENDEREC
      cGetBairro := SZ1 -> Z1_BAIRRO
      cImovel := SZ1 -> Z1_TIPOIMO
      cGetCEP := SubStr(SZ1 -> Z1_CEP,1,5)+'-'+SubStr(SZ1 -> Z1_CEP,6,3)
      cEstados := SZ1 -> Z1_ESTADO
      cGetCidade := SZ1 -> Z1_CIDADE
      cEstCivil := SZ1 -> Z1_ESTADOC
      nPet := Val(SZ1 -> Z1_TEMPET)
      lCheckADVPL := if(SZ1 -> Z1_LADVPL = '1', .T., .F.)
      lCheckSQL := if(SZ1 -> Z1_LSQL = '1', .T., .F.)
      lCheckCSHARP := if(SZ1 -> Z1_LCSHARP = '1', .T., .F.)
      lCheckPython := if(SZ1 -> Z1_PYTHON = '1', .T., .F.)
      cObservacao := SZ1 -> Z1_OBS
    else
      MsgInfo('Cliente não encontrado', 'Atenção')
    endif
    
    DbCloseArea()
return

Static Function AlterarSZ1()

  DbSelectArea('SZ1')
  DbSetOrder(1)
  
  if DbSeek(XFILIAL('SZ1')+cGetCodigo)
    RecLock('SZ1', .F.)
      SZ1 -> Z1_NOME := cGetNome
      SZ1 -> Z1_NOMERED := cGetNomeReduz
      SZ1 -> Z1_DATNASC := dGetDatNasc
      SZ1 -> Z1_DDDCEL :=  STRTRAN(STRTRAN(cGetDDDCel,'(',''),')','')
      SZ1 -> Z1_CELULAR := STRTRAN(cGetCEL,'-','')
      SZ1 -> Z1_SEXO := CValToChar(nSexo)  // 1 = Masculino / 2 = Feminino
      SZ1 -> Z1_CPF := STRTRAN(STRTRAN(cGetCPF,'.',''),'-','')
      SZ1 -> Z1_ENDEREC := cGetEndereco
      SZ1 -> Z1_BAIRRO := cGetBairro
      SZ1 -> Z1_TIPOIMO := cImovel
      SZ1 -> Z1_CEP := STRTRAN(cGetCEP,'-','')
      SZ1 -> Z1_ESTADO := cEstados
      SZ1 -> Z1_CIDADE := cGetCidade
      SZ1 -> Z1_ESTADOC := cEstCivil
      SZ1 -> Z1_TEMPET := CValToChar(nPet)
      SZ1 -> Z1_LADVPL := IIF(lCheckADVPL = .T.,"1","2") // 1 = sim, 2 = não
      SZ1 -> Z1_LSQL := IIF(lCheckSQL,"1","2")
      SZ1 -> Z1_LCSHARP := IIF(lCheckCSHARP,"1","2")
      SZ1 -> Z1_PYTHON := IIF(lCheckPython,"1","2")
      SZ1 -> Z1_OBS := cObservacao
    MsUnlock()

    MsgInfo('Cliente alterado com sucesso', 'Sucesso')
  else
    MsgInfo('Cliente não encontrado', 'Atenção')
  endif

return

Static Function ExcluirSZ1()

  if(MsgYesNo('Deseja mesmo excluir o registro de '+cGetNome+'?', 'Atenção'))
    DbSelectArea('SZ1')
    DbSetOrder(1)
    
    if DbSeek(XFILIAL('SZ1')+cGetCodigo)
      RecLock('SZ1', .F.)
        DbDelete()
      MsUnlock()
      
      MsgInfo('Cliente deletado com sucesso', 'Sucesso')
      Limpartela()
    else
      MsgInfo('Cliente não encontrado', 'Atenção')
    endif
  else
      Alert('Operação cancelada pelo usuário')
  endif

  DbCloseArea()
return

Static Function Limpartela()

  cGetCodigo := Space(6)
  cGetNome := Space(30)
  cGetNomeReduz := Space(20)
  dGetDatNasc := Date()
  cGetDDDCel := Space(4)
  cGetCEL := Space(11)
  nSexo := 2
  cGetCPF := Space(14)
  cGetEndereco := Space(30)
  cGetBairro := Space(20)
  cImovel := aImoveis[2]
  cGetCEP := Space(9)
  cEstados := aEstados[5]
  cGetCidade := Space(30)
  cEstCivil := aEstCivil[4]
  nPet := 2
  lCheckADVPL := .F.
  lCheckSQL := .F.
  lCheckCSHARP := .F.
  lCheckPython := .F.
  cObservacao := "Digite aqui as características de Protheuzeiro que te definem"

return
